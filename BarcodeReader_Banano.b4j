AppType=JavaFX
Build1=Default,b4j.example
File1=Layout1.bjl
File2=Modal1.bjl
File3=scanner.html
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
Group=Default Group
Library1=banano
Library2=jcore
Library3=jfx
Library4=jxui
Library5=bananoskeleton
Module1=BANanoFiles
Module2=BarcodeReader
Module3=BarcodeScanner
Module4=Point2D
Module5=TextResult
NumberOfFiles=3
NumberOfLibraries=5
NumberOfModules=5
Version=8.8
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region

Sub Process_Globals
	Private BANano As BANano 'ignore
	Private decodeButton As SKButton 'ignore
	Private loadImageButton As SKButton 'ignore
	Private codeImage As SKImage 'ignore
	Private bf As BANanoFiles
	Private SKNavigationBar1 As SKNavigationBar 'ignore
	Private reader As BarcodeReader
	Private scanner As BarcodeScanner
	Private resultContainer As SKContainer 'ignore
	Private Dynamsoft As BANanoObject
	Private SKModal1 As SKModal 'ignore
	Private resultLabel As SKLabel 'ignore
End Sub

Sub AppStart (Form1 As Form, Args() As String) 'ignore
	' you can change some output params here
	BANano.Initialize("BANano", "BANanoSkeletonDemo",2)
	BANano.HTML_NAME = "index.html"
	
	BANano.Header.Title="BANano Skeleton Demo"
	BANano.Header.AddJavascriptFile("https://cdn.jsdelivr.net/npm/dynamsoft-javascript-barcode@8.2.3/dist/dbr.js")
	BANano.TranspilerOptions.ExternalTestConnectionServer = "http://gorgeousapps.com"	
	' start the build
	BANano.Build(File.DirApp)
		
	ExitApplication
End Sub

' HERE STARTS YOUR APP
Sub BANano_Ready()
	BANano.LoadLayout("#body","Layout1")
	Dim body As BANanoElement
	body=BANano.GetElement("#body")	
	body.Append($"<div id="modalHolder"></div>"$).Get("#modalHolder").LoadLayout("Modal1")
	SKNavigationBar1.AddMenu("barcodereader", "Barcode Reader")
	bf.Initialize.AddFileSelect(Me, "body", "fuconnect")
	Dynamsoft.Initialize("Dynamsoft")
	Dynamsoft.GetField("DBR").GetField("BarcodeReader").SetField("productKeys","t0077xQAAAD5PnCqP+JzPKuMXcREgPtAcHe3Xuc8Fxzj9yQCaSHmFE9cAhcU9TgabPY+I8ZBm85DEPh7H4WgYtlP954MeI92qtGrzAQ6NKXE=")
	Dynamsoft.GetField("DBR").GetField("BarcodeScanner").SetField("defaultUIElementURL","assets/scanner.html")
	reader.Initialize(Dynamsoft)
	scanner.Initialize(Dynamsoft,"scanner")
End Sub

private Sub scanner_onUnduplicatedRead(txt As String,result As TextResult)
	Log("UnduplicatedRead")
	resultLabel.Text=result.Text
	SKModal1.Open	
End Sub

private Sub scanner_onFrameRead(results() As TextResult)

End Sub

Private Sub loadImageButton_Click (event As BANanoEvent)
	resultContainer.Element.SetHTML("")
	bf.ShowFileSelect("fuconnect")
End Sub

Private Sub decodeButton_Click (event As BANanoEvent)
	resultContainer.Element.SetHTML("Decoding...")
	Dim results As List=BANano.Await(reader.decode(codeImage.Src))
	Dim sb As StringBuilder
	sb.Initialize	
	Dim index As Int
	For Each tr As TextResult In results		
		index=index+1
		sb.Append(index).Append(". ")		
		sb.Append(tr.Text)
		sb.Append("<br/>")		
	Next
	resultContainer.Element.SetHTML(sb.ToString)
End Sub

Private Sub scanButton_Click (event As BANanoEvent)
	scanner.show
End Sub

Sub fuconnect_change(e As BANanoEvent)
	Dim fileList As List = bf.GetFileListFromTarget(e)
	'no file is selected
	If fileList.size = 0 Then Return
	'only process 1 file
	Dim fr As String = fileList.get(0)
	'
	Dim Result As Map
	Dim promise As BANanoPromise = bf.readAsDataURL(fr)
	promise.Then(Result)
	'get the json content
	Dim data As String = Result.get("result")
	
	codeImage.Src=data
	codeImage.Alt="code"
	'
	promise.Else(Result)
	Dim compError As String = Result.get("result")
	Log(compError)
	promise.End	
	'nully file component so we can select same file
	bf.Nullify
End Sub

Private Sub SKModal1_CancelClick (event As BANanoEvent)
	
End Sub

Private Sub SKModal1_OKClick (event As BANanoEvent)
	
End Sub